t2t {
  main = parameterDef* rewriteDef

  parameterDef = "%" s_ "parameter" s_ name s_
  rewriteDef = "%" s_ "rewrite" s_ name s_ "{" s_ rewriteRule+ s_ "}" s_

  rewriteRule = ruleName s_ "[" s_ (argDef s_)* "]" s_ "=" s_ rewriteScope s_

  argDef = 
    | "(" (name s_)+ ")" ("+" | "*" | "?") -- parenthesized
    | name ("+" | "*" | "?")               -- iter
    | name                                 -- plain

  rewriteScope =
    | "⎡" s_ binding s_ rewriteScope s_ "⎦" -- scope
    | rewriteFormatString                   -- plain
  
  binding =
    | "⎨" s_ name s_ rewriteFormatString s_ "⎬" -- call
    | name s_ "=" s_ rewriteFormatString        -- parameterAssignment

  rewriteFormatString = "‛" formatItem* "’"
  formatItem =
    | "⎨" s_ name s_ (rewriteFormatString s_)+ "⎬" -- supportCall
    | "⟪" parameterRef "⟫"                         -- parameter
    | "«" argRef "»"                               -- arg
    | "\\" any                                     -- escapedCharacter
    | ~"‛" ~"’" ~"⎡" ~"⎦" ~"⟪" ~"⟫" ~"«" ~"»" any  -- rawCharacter

  argRef = name
  parameterRef = name
  ruleName = name

  name (a name)
    = nameFirst nameRest*
  nameFirst = ("_" | letter)
  nameRest  = ("_" | alnum)

  s_ = space*

}
